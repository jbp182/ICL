PARSER_BEGIN(Parser)


/** ID lister. */
package Components;
import LanguageComponents.Environments.CodeBlock;
import LanguageComponents.Environments.CompilerEnvironment;
import LanguageComponents.Environments.InterpreterEnvironment;
import LanguageComponents.Nodes.*;
import LanguageComponents.Nodes.ArithmeticOperators.*;
import LanguageComponents.Nodes.FuncionalCore.*;
import LanguageComponents.Nodes.ImperativeCore.*;
import LanguageComponents.Nodes.LogicOperators.*;
import LanguageComponents.Nodes.RelationalOperators.*;
import java.io.FileInputStream;
import java.io.File;
import java.util.LinkedList;

public class Parser {

  /** Main entry point. */
  public static void main(String args[]) throws  Exception {

    if(args.length > 1 && args[0].equals("-f")){
        compile(args[1]);
        return;
    }

    Parser parser = new Parser(System.in);
    ASTNode exp;

    while (true) {
        try {
        exp = parser.Start();
        exp.eval(new InterpreterEnvironment(null)).show();
        } catch (Exception e) {
          System.out.println ("Syntax Error!\n" + e.getMessage());
          parser.ReInit(System.in);
        }
    }
  }

  private static void compile(String fileName) throws Exception{
      File f = new File(fileName);
      Parser parser = new Parser(new FileInputStream(f));
      ASTNode exp = parser.Start();
      CodeBlock blk = new CodeBlock();
      exp.compile(new CompilerEnvironment(null),blk);
      blk.dump("Main.j");
  }

}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
}

TOKEN :
{
  < Num: (["0"-"9"]) + >
  |
  < PLUS : "+" >
  |
  < MINUS : "-">
  |
  < TIMES : "*">
  |
  < DIV : "/">
  |
  < LPAR : "(" >
  |
  < RPAR : ")" >
  |
  < EL: "\n" >
  |
  < LET: "let" >
  |
  < IN: "in" >
  |
  < END: "end" >
  |
  < COMMA: "," >
  |
  < NOT: "~" >
  |
  < ISEQ: "==" >
  |
  < AND: "&&" >
  |
  < OR: "||" >
  |
  < GT: ">" >
  |
  < LT: "<" >
  |
  < GE: ">=" >
  |
  < LE: "<=" >
  |
  < EQ: "=" >
  |
  < NEW: "new" >
  |
  < SEQ: ";" >
  |
  < ASSIGN: ":=" >
  |
  < DESREF: "!" >
  |
  < IF: "if" >
  |
  < THEN: "then" >
  |
  < ELSE: "else" >
  |
  < WHILE: "while" >
  |
  < DO: "do" >
  |
  < BOOL: ("true"|"false") >
  |
  < FUN: "fun" >
  |
  < IS: "->" >
  |
  < COLON: ":" >
  | 
  < Id: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"] )* >
}

ASTNode Start():
{ ASTNode t; }
{
   ( t = EM() <EL> )
   { return t; }
}

ASTNode EM():
{ Token op;
  ASTNode t1, t2; }
{
  t1 = E() ( ( op=< SEQ >) t2=EM()
  				{ t1 = new ASTSeq(t1,t2); }
			)*
	{ return t1; }
}

ASTNode E():
{ Token op;
  ASTNode t1, t2; }
{
  	t1 = EA() ( op=< ISEQ > t2=EA()
  				{ t1 = new ASTEquals(t1,t2); }
  			  )?
    { return t1; }
}
    
  

ASTNode EA() :
{ Token op;
  ASTNode t1, t2; }
{
     t1=T() ( ( op=<PLUS> | op=<MINUS> | op=<AND> ) t2=EA()
                 { if (op.kind == PLUS) 
                         t1 = new ASTPlus(t1,t2);
                   else if (op.kind == MINUS)
                   		 t1 = new ASTSub(t1,t2);
                   else t1 = new ASTAnd(t1,t2);
                 } 
                  ) *
     { return t1; } 
}

ASTNode T() :
{ Token op;
  ASTNode f1, f2;
  LinkedList al;}
{
     f1=F() ( ( < LPAR > al=AL() < RPAR >
				{ f1 = new ASTApply(f1, al); }
     			)+
       			| < ASSIGN > f2=E() { f1 = new ASTAssign(f1,f2); }
    			| ( ( op=<GE> | op=<LE> | op=<GT> | op=<LT> ) f2=F()
	   				{ if (op.kind == GE)
	   						f1 = new ASTGreaterEqual(f1,f2);
	   				  else if (op.kind == LE)
	   				  		f1 = new ASTLowerEqual(f1,f2);
	   				  else if (op.kind == GT)
	   						f1 = new ASTGreater(f1,f2);
	   				  else if (op.kind == LT)
	   				  		f1 = new ASTLower(f1,f2);
	   				}
				   )
     	        | ( ( op=<TIMES> | op=<DIV> | op=<OR> ) f2=T()
			     { if (op.kind == TIMES)
			     		f1 = new ASTMul(f1,f2);
			     	else if (op.kind == DIV)
			     		f1 = new ASTDiv(f1,f2);
			     	else f1 = new ASTOr(f1,f2);
			   	 }
			       )*
			   )
	{ return f1; }
}

LinkedList AL():
{ ASTNode n;
  LinkedList<ASTNode> args = new LinkedList(); }
{
  ( n=EM()
  	{ args.add(n); }
  	( < COMMA > n = EM()
  		{ args.add(n); }
 	)* )?
 { return args; }
}


LinkedList PL():
{ Token id, type;
  LinkedList<ASTNode> ids_types = new LinkedList(); }
{
  (id=<Id><COLON > type=<Id>
  		{ ids_types.add( new ASTId(id.image, type.image) ); }
   	 ( id=< Id ><COLON > type=< Id > 
  		{ ids_types.add( new ASTId(id.image, type.image) ); }
  	 )* )?
  	 { return ids_types; }
}

ASTNode F() :
{ Token n, id, id1, id2; 
  ASTNode t, init1, init2, body;
  LinkedList<String> ids;
  LinkedList<ASTNode> inits;}
{
    (  n=< Num >   {  t = new ASTNum(Integer.parseInt(n.image)); }
   | <LPAR> t=EM() <RPAR>
   | < MINUS > t=F() { t = new ASTMul(new ASTNum(-1), t); }
   | < PLUS > t=F()
   | n =< BOOL > { t = new ASTBool( n.image ); }
   | < NEW > t=F() { t = new ASTNewRef( t ); }
   | < DESREF > t=F() { t = new ASTDeref( t ); }
   | < IF > t=EM() < THEN > init1=EM() < ELSE > init2=EM() < END >
   			{ t = new ASTIf( t, init1, init2 ); }
   | < WHILE > t=EM() < DO > body=EM() < END >
   			{ t = new ASTWhile(t,body); }
   | < NOT > t=F()
   			{  t = new ASTNeg(t); }
   | < LET > id1 = < Id > <EQ > init1=EM()
   			{	ids = new LinkedList();
   		  		inits = new LinkedList();
   		  		ids.add(id1.image); inits.add(init1); }
   		( < COMMA > id2 = < Id > <EQ > init2=EM()
   			{ ids.add(id2.image); inits.add(init2); }
   		)*
   		< IN > body=EM() < END >
   		
   		{ t = new ASTLet(ids, inits, body); }
    | <FUN> inits=PL() <IS> body=EM() <END>
    	{ t = new ASTFun(inits, body); }
   	| id=< Id > { t = new ASTId( id.image ); }
   )

   { return t; }
}




















